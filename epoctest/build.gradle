apply plugin: 'com.android.library'
apply plugin: 'com.google.protobuf'

android {
    compileSdkVersion 26
    //buildToolsVersion "26.0.2"
    defaultConfig {
        minSdkVersion 22
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++11"
            }
        }
    }
    splits {
        abi {
            enable true
            reset()
            include 'arm64-v8a'
            universalApk false
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
    sourceSets {
        main {
            proto {
                srcDir 'src/proto'
            }
            java {
                srcDir 'src/main/java'
            }
        }
        test {
            proto {
                srcDir 'src/proto'
            }
            java {
                srcDir 'src/test/java'
            }
        }
    }
    packagingOptions {
        exclude "/META-INF/**"
        merge "plugin.xml"
    }
}
protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.0.0"
    }
    plugins {
        lite {
            artifact = "com.google.protobuf:protoc-gen-javalite:3.0.0"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                remove java
            }
            task.plugins {
                lite { }
            }
        }
    }
}

dependencies {
    api fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:26.0.2'
    // dagger2
    implementation 'com.google.dagger:dagger:2.9'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.9'
    // protobuf
    implementation 'com.google.protobuf:protobuf-gradle-plugin:0.8.7'
    implementation 'com.google.protobuf:protobuf-lite:3.0.0'

    annotationProcessor 'org.eclipse.sisu:org.eclipse.sisu.inject:0.0.0.M5'

    implementation project(':util')
    implementation project(':network')
    implementation project(':common')
    implementation project(':datamanager')
    implementation project(':epoclog')
    implementation project(':communicationprotocol')

    // Required -- JUnit 4 framework
    testImplementation 'junit:junit:4.12'
    // Optional -- Mockito framework
    testImplementation 'org.mockito:mockito-core:2.7.22'
    // Optional -- Hamcrest matchers
    testImplementation 'org.hamcrest:hamcrest-library:1.3'

}
